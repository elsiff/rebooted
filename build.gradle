buildscript {
    ext.kotlin_version = '1.3.41'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
    }
}

configurations {
    testCompile.extendsFrom compileOnly
}

allprojects {
    group 'me.elsiff'
    version '1.0.0-SNAPSHOT'
}

subprojects {
    apply plugin: "kotlin"
    apply plugin: "com.github.johnrengelman.shadow"

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-M2'
        testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
        testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"

        implementation 'ch.qos.logback:logback-classic:1.2.3'
    }

    build {
        dependsOn shadowJar
    }
}

project(':rebooted-engine-lwjgl') {
    dependencies {
        implementation project(':rebooted-engine')
    }
}

project(':rebooted-game') {
    dependencies {
        implementation project(':rebooted-engine')
        implementation project(':rebooted-engine-lwjgl')
    }
}